plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id "io.freefair.lombok" version '5.3.0'
}

group 'dev.dmcode'
version '0.1'
archivesBaseName = 'kafka-test-broker'

repositories {
    mavenCentral()
}

def kotestVersion = '4.3.1'
def kafkaClientsVersion = '2.6.0'
def slf4jVersion = '1.7.30'
def jacksonVersion = '2.11.3'
def springTestKafkaVersion = '2.6.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    compileOnly "org.slf4j:slf4j-api:$slf4jVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-assertions-core:$kotestVersion"
    testImplementation "io.kotest:kotest-property:$kotestVersion"
    testImplementation "org.apache.kafka:kafka-clients:$kafkaClientsVersion"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
    testImplementation "org.springframework.kafka:spring-kafka-test:$springTestKafkaVersion"

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

install.repositories.mavenInstaller.pom*.whenConfigured { pom ->
    pom.dependencies = Collections.emptyList()
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Kafka Test Broker'
                packaging 'jar'
                url 'https://github.com/dmcodev/kafka-test-broker'

                scm {
                    connection 'scm:git@github.com:dmcodev/kafka-test-broker.git'
                    developerConnection 'scm:git@github.com:dmcodev/kafka-test-broker.git'
                    url 'git@github.com:dmcodev/kafka-test-broker.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/dmcodev/kafka-test-broker/blob/master/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        id 'dmcodev'
                        name 'Damian Malczyk'
                        email 'damian.malczyk.dev@gmail.com'
                    }
                }
            }
        }
    }
}